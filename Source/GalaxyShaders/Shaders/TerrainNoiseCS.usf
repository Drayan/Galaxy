#include "FastNoiseLite.ush"
#include "/Engine/Public/Platform.ush"

RWTexture2D<float> OutHeight;
cbuffer Params
{
    float Frequency;
    float Amplitude;
    float Lacunarity;
    float Persistence;
    int Octaves;
    
    float Radius;
    float2 PatchMin;
    float2 PatchMax;
    float3 PlanetCenter;
    float3 FaceX;
    float3 FaceY;
    float3 FaceZ;
    
    uint Seed;
};

static fnl_state noise = fnlCreateState(Seed);

[numthreads(8, 8, 1)]
void MainCS(uint3 DTid : SV_DispatchThreadID)
{
    uint2 dim;
    OutHeight.GetDimensions(dim.x, dim.y);
    if(DTid.x >= dim.x || DTid.y >= dim.y)
    {
        return;
    }
    
    // 1. Map 2D texture coordinates to a point on the cube face
    float2 uv = (float2) DTid.xy / (float2) (dim - 1);
    float2 uvPatch = lerp(PatchMin, PatchMax, uv);
    
    // 2. Convert to 3D position on the cube face
    float3 pCube = FaceX * uvPatch.x + FaceY * uvPatch.y + FaceZ;
    
    // 3. Project onto sphere
    float3 dir = normalize(pCube - PlanetCenter);
    float3 pWorld = PlanetCenter + dir * Radius;
    
    // 4. Generate fractal noise
    noise.noise_type = FNL_NOISE_OPENSIMPLEX2;
    float sum = 0, a = 1, f = Frequency;
    //[unroll]
    for (int i = 0; i < Octaves; ++i)
    {
        sum += a * fnlGetNoise3D(noise, pWorld.x * f, pWorld.y * f, pWorld.z * f);
        f *= Lacunarity;
        a *= Persistence;
    }

    OutHeight[DTid.xy] = sum * Amplitude;
}